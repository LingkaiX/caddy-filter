import org.echocat.gradle.plugins.golang.tasks.GolangTask
import org.echocat.gradle.plugins.golang.utils.Executor
import static org.echocat.gradle.plugins.golang.model.Platform.currentPlatform

plugins {
    id 'org.echocat.golang' version '0.1.16'
}

group 'github.com/echocat/caddy-filter'

golang {
    // For testing only the current platform is required.
    platforms = currentPlatform()
    dependencies {
        build 'github.com/mholt/caddy:v0.10.4'
        test 'github.com/stretchr/testify'
        tool 'github.com/mattn/goveralls'
    }
    build {
        useTemporaryGopath = true
    }
    testing {
        coverProfile = "${buildDir}/testing/${name}.cover"
        coverProfileHtml = "${buildDir}/testing/${name}.cover.html"
        arguments = ["-check.vv"]
    }
    toolchain {
        goversion = 'go1.8'
    }
}

task baseBuild(type: GolangTask, overwrite: true) {
    action {
        throw new UnsupportedOperationException("This project only supports test task.");
    }
}

task uploadCoverage(type: GolangTask, group: "verification", description: "Upload test coverage report to coveralls.io.", dependsOn: "baseTest") {
    action {
        final token = System.getenv("COVERALLS_TOKEN")
        if (!token) {
            logger.info("No COVERALLS_TOKEN provided. Will skip upload coverage report.")
            return
        }
        Executor.executor("${project.buildDir}/tools/github.com/mattn/goveralls${build.currentPlatformExtension}", logger)
                .argument("-coverprofile=${project.buildDir}/testing/${project.name}.cover")
                .argument("-service=circle-ci")
                .argument("-repotoken=${token}")
                .env("GOROOT", toolchain.goroot)
                .env("GOPATH", build.gopath)
                .execute()
    }
}

test.dependsOn("uploadCoverage")
